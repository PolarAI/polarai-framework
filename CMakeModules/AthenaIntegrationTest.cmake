set(INTEG_RUN_SCRIPT_NAME "integration_test_run.py")

function(add_athena_integration_test)
    cmake_parse_arguments(PARSED_ARGS "" "TARGET_NAME" "SRCS;LIBS;CONF_STRS" ${ARGN})
    set(INTEG_CONFIG_INNER_NAME "${PARSED_ARGS_TARGET_NAME}.conf")
    set(INTEG_CONFIG_OUTER_NAME "${PARSED_ARGS_TARGET_NAME}.xml")
    add_executable(${PARSED_ARGS_TARGET_NAME} ${modifier} ${PARSED_ARGS_SRCS})
    athena_disable_rtti(${PARSED_ARGS_TARGET_NAME})
    athena_disable_exceptions(${PARSED_ARGS_TARGET_NAME})
    if(PARSED_ARGS_LIBS)
        target_link_libraries(${PARSED_ARGS_TARGET_NAME} ${PARSED_ARGS_LIBS})
    endif()
    if(PARSED_ARGS_CONF_STRS)
        foreach(KeyAndValue ${PARSED_ARGS_CONF_STRS})
            string(REGEX MATCH "^[^=]+" Key ${KeyAndValue})
            string(REPLACE "${Key}=" "" Value ${KeyAndValue})
            set(${Key} ${Value})
        endforeach()
    endif()
    if(EXISTS ${CMAKE_CURRENT_SOURCE_DIR}/${INTEG_CONFIG_INNER_NAME})
        configure_file(${INTEG_CONFIG_INNER_NAME} ${INTEG_CONFIG_OUTER_NAME})
    endif()
    set(COMMAND_RUN_INTEGRATION_TEST python3 ../${INTEG_RUN_SCRIPT_NAME} ${PARSED_ARGS_TARGET_NAME})
    add_custom_command(OUTPUT run_test_script COMMAND ${COMMAND_RUN_INTEGRATION_TEST})
    add_custom_target(${PARSED_ARGS_TARGET_NAME}-run ALL DEPENDS ${PARSED_ARGS_TARGET_NAME} run_test_script)
    add_test(NAME ${PARSED_ARGS_TARGET_NAME} COMMAND ${COMMAND_RUN_INTEGRATION_TEST})
endfunction(add_athena_integration_test)
